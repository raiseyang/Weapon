apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// 读取属性值
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

//项目的git地址
def gitUrl = 'https://github.com/raiseyang/Weapon.git'
//发布到组织名称名字，必须填写
group = "com.raiseyang.weapon"
//发布到JCenter上的项目名字
def libName = "weapon-base"
// 版本号，下次更新是只需要更改版本号即可
version = rootProject.ext.weapon_version_name
//compile 'com.raiseyang.weapon:weapon-base:0.0.1'

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

//生成Javadoc文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.author true
    options.version true
    failOnError false
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

//拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

//上传到JCenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name ''
                developers {
                    developer {
                        id properties.getProperty("bintray.user")
                        name properties.getProperty("bintray.user")
                        email properties.getProperty("bintray.email")
                    }
                }
            }
        }
    }
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.key")
    configurations = ['archives']
    pkg {
        repo = 'maven' // 仓库名称,可以在一个仓库下面发布多个开源库
        name = libName // 项目名称
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
    }
    publish = true
}